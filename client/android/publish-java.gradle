import com.yandex.div.gradle.PublicationType

apply plugin: 'maven-publish'

group = 'com.yandex.div'

def gitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def publicationType = PublicationType.fromString(rootProject.findProperty("publicationType"))

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier "sources"
}

// Because the components are created only during the afterEvaluate phase, you must configure
// your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.java
                artifact sourcesJar
                version divkitVersionName
                if (project.hasProperty('teamcity.version')) {
                    pom.description = "Built on commit: ${gitHash()}"
                }
                ext.repo = 'release'
            }
        }

        repositories {
            maven {
                name 'artifactory'
                credentials {
                    username 'yandexmobiledeploy'
                    password 'yandexmobiledeploy'
                }
                url = publicationType.artifactoryUrl
            }
        }
    }
}
